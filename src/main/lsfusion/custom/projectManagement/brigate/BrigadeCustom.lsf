MODULE BrigadeCustom;

REQUIRE ProjectCustom, BrigadeEmployeeRoleCustom;

NAMESPACE ProjectManagement;

CLASS Brigade 'Бригада';
CLASS BrigadeEmployee 'Сотрудник бригады';

name '{Name}' = DATA ISTRING[100] (Brigade) CHARWIDTH 15;

project 'Проект' = DATA Project (Brigade) NONULL;
numberProject 'Проект' (Brigade b) = number(project(b));
index '№' = PARTITION SUM 1 ORDER Brigade b BY project(b) IN id MATERIALIZED CHARWIDTH 3;

manager 'Руководитель' = DATA Employee (Brigade) NONULL;
nameManager 'Руководитель' (Brigade b) = name[Partner](manager(b));

brigade 'Бригада' = DATA Brigade(BrigadeEmployee) NONULL DELETE;
index '№' = PARTITION SUM 1 ORDER BrigadeEmployee be BY brigade(be) IN id MATERIALIZED CHARWIDTH 3;

employee 'Сотрудник' = DATA Employee(BrigadeEmployee) NONULL;
nameEmployee 'Сотрудник' (BrigadeEmployee be) = name[Partner](employee(be));

brigadeEmployeeRole 'Роль' = DATA BrigadeEmployeeRole (BrigadeEmployee);
nameBrigadeEmployeeRole 'Роль' (BrigadeEmployee be) = name(brigadeEmployeeRole(be));

project (BrigadeEmployee be) = project(brigade(be));

FORM brigade 'Бригада'
    OBJECTS b = Brigade PANEL
    PROPERTIES (b) name, numberProject, nameManager    

    OBJECTS be = BrigadeEmployee
    PROPERTIES (be) index, nameEmployee, nameBrigadeEmployeeRole,
        NEW, DELETE
    FILTERS brigade(be) = b
    
    EDIT Brigade OBJECT b
;

EXTEND FORM project
    OBJECTS b = Brigade
    PROPERTIES (b) name, nameManager
    PROPERTIES (b) NEW, DELETE
    FILTERS project(b) == p

    OBJECTS be = BrigadeEmployee
    PROPERTIES (be) index, nameEmployee, nameBrigadeEmployeeRole,
        NEW SHOWIF b, DELETE SHOWIF b
    FILTERS brigade(be) = b
;
DESIGN project{
    details {
        NEW brigade {
            caption = 'Бригады';
            fill = 1;
            horizontal = TRUE;

            MOVE BOX(b) {
                fill = 2;
                caption = '';
                
            }
            MOVE BOX(be) {
                fill = 2;
                caption = 'Сотрудники';
            }            
        }
    }
}

@defineProjectHistoryLine(project, Brigade, manager, 'Изменен руководитель бригады', nameManager);

description = ABSTRACT STRING (Brigade);

@defineProjectHistoryLineProperty(project, Brigade, name, 'Изменено наименование бригады', description);

@defineProjectHistoryLine(project, BrigadeEmployee, employee, 'Изменен cотрудник бригады', nameEmployee);

description = ABSTRACT STRING (BrigadeEmployee);

@defineProjectHistoryLineProperty(project, BrigadeEmployee, nameBrigadeEmployeeRole, 'Изменена роль сотрудника', description);