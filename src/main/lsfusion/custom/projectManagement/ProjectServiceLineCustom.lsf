MODULE ProjectServiceLineCustom;

REQUIRE ProjectCustom, ServiceP;

NAMESPACE ProjectManagement;

CLASS ProjectServiceLine 'Строка услуг проекта';

project = DATA Project (ProjectServiceLine) NONULL DELETE;
index '№' = PARTITION SUM 1 ORDER ProjectServiceLine l BY project(l) IN id MATERIALIZED CHARWIDTH 3;

service = DATA Service (ProjectServiceLine) NONULL;
nameService 'Service' (ProjectServiceLine l) = name(service(l));

dataUom = DATA Uom (ProjectServiceLine);
uom (ProjectServiceLine l) = OVERRIDE dataUom(l), uom(service(l));
nameUom 'UoM' (ProjectServiceLine l) = name(uom(l));

initialDemand 'Initial demand' = DATA NUMERIC[16,3] (ProjectServiceLine);

EXTEND FORM project
    OBJECTS sl = ProjectServiceLine
    PROPERTIES(sl) index, nameService, nameUom, initialDemand
    PROPERTIES(sl) NEW, DELETE
    FILTERS project(sl) = p
;

DESIGN project{
    details {        
        MOVE BOX(sl) {
            caption = 'Работы';
        }        
    }
}

@defineProjectHistoryLine(project, ProjectServiceLine, service);
@defineProjectHistoryLineProperty(project, ProjectServiceLine, uom, 'Работа - Ед. изм.', nameService);
@defineProjectHistoryLineProperty(project, ProjectServiceLine, initialDemand, 'Работа - Планируемое кол-во', nameService);
