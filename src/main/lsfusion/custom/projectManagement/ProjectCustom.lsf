MODULE ProjectCustom;

REQUIRE Project, ProjectLegalEntityRoleCustom;

NAMESPACE ProjectManagement;

contractStartDate 'Договорные сроки начала Контракта' = DATA DATE (Project);
contractFinishDate 'Договорные сроки окончания Контракта' = DATA DATE (Project);
planStartDate 'Плановый срок начала' = DATA DATE (Project);
planFinishDate 'Плановый срок окончания' = DATA DATE (Project);
factStartDate 'Фактический срок начала' = DATA DATE (Project);
factFinishDate 'Фактический срок окончания' = DATA DATE (Project);
contractDate 'Дата договора' = DATA DATE (Project);
projectAdmin 'Администратор проекта' = DATA Employee	(Project);
nameProjectAdmin 'Администратор проекта' (Project o) = MasterData.name(projectAdmin(o));

warrantyObligations 'Гарантийные обязательства' = DATA BOOLEAN (Project);
warrantyStartDate 'Срок начала действия гарантии'	 = DATA DATE (Project); //при установке признака Гарантийные обязательства
warrantyFinishDate 'Срок окончания действия гарантии' = DATA DATE (Project); //при установке признака Гарантийные обязательства

CONSTRAINT SETCHANGED(number(Project p)) AND (GROUP SUM 1 IF number(p) == number(Project pp) AND NOT p == pp)
    MESSAGE 'Совпадение номера проекта';

// история
CLASS ProjectHistory 'Change history';

project = DATA Project (ProjectHistory) INDEXED;
countHistories (Project d) = GROUP SUM 1 IF project(ProjectHistory h) = d;

dateTime 'Date' = DATA DATETIME (ProjectHistory);

user = DATA User (ProjectHistory);
nameUser 'User' (ProjectHistory l) = name(user(l));

computer = DATA Computer (ProjectHistory);
hostnameComputer 'Computer' (ProjectHistory l) = hostname(computer(l)) CHARWIDTH 10;

type 'Type' = DATA ISTRING[100] (ProjectHistory) CHARWIDTH 15;
description 'Description' = DATA TEXT (ProjectHistory);

@defineHistoryProperty(project, type, 'Тип', nameType);
@defineHistoryProperty(project, status, 'Статус', nameStatus);
@defineHistoryProperty(project, partner, 'Контрагент', namePartner);
@defineHistoryProperty(project, company, 'Компания', nameCompany);
@defineHistoryProperty(project, manager, 'Менеджер', nameManager);
@defineHistoryProperty(project, projectAdmin, 'Администратор проекта', nameProjectAdmin);

@defineHistoryProperty(project, number, 'Номер');
@defineHistoryProperty(project, name, 'Имя');
@defineHistoryProperty(project, archived, 'Архивный');
@defineHistoryProperty(project, endDate, 'Дата архивирования');
@defineHistoryProperty(project, contractDate, 'Дата договора');
@defineHistoryProperty(project, contractStartDate, 'Договорные сроки начала Контракта');
@defineHistoryProperty(project, contractFinishDate, 'Договорные сроки окончания Контракта');
@defineHistoryProperty(project, planStartDate, 'Плановый срок начала');
@defineHistoryProperty(project, planFinishDate, 'Плановый срок окончания');
@defineHistoryProperty(project, factStartDate, 'Фактический срок начала');
@defineHistoryProperty(project, factFinishDate, 'Фактический срок окончания');
@defineHistoryProperty(project, warrantyObligations, 'Гарантийные обязательства');
@defineHistoryProperty(project, warrantyStartDate, 'Срок начала действия гарантии');
@defineHistoryProperty(project, warrantyFinishDate, 'Срок окончания действия гарантии');

@defineDocHistoryForm(project, project, p);

changeDate 'Дата изменения' (Project p) = GROUP LAST dateTime(ProjectHistory ph) IF project(ph) = p ORDER dateTime(ph), ph;

CLASS ProjectLegalEntity 'Кортагент проекта';

project 'Проект' = DATA Project(ProjectLegalEntity) NONULL DELETE;
index '№' = PARTITION SUM 1 ORDER ProjectLegalEntity le BY project(le) IN id MATERIALIZED CHARWIDTH 3;

legalEntity 'Кортагент проекта' = DATA LegalEntity (ProjectLegalEntity) NONULL;
nameLegalEntity 'Кортагент проекта' (ProjectLegalEntity le) = name(legalEntity(le));

projectLegalEntityRole 'Роль в бригаде' = DATA ProjectLegalEntityRole (ProjectLegalEntity);
nameProjectLegalEntityRole 'Роль в бригаде' (ProjectLegalEntity le) = name(projectLegalEntityRole(le));

countProjectLegalEntities (Project p) = GROUP SUM 1 IF project(ProjectLegalEntity le) = p;

EXTEND FORM projects
    PROPERTIES (p) READONLY changeDate, contractDate,
        contractStartDate, contractFinishDate, planStartDate, planFinishDate, factStartDate, factFinishDate,
        nameProjectAdmin, 
        warrantyObligations, warrantyStartDate, warrantyFinishDate
;

DESIGN projects{
    PROPERTY(namePartner(p)) {caption = 'Заказчик';};
}

EXTEND FORM project
    PROPERTIES (p) changeDate READONLY,
        contractStartDate, contractFinishDate, planStartDate, planFinishDate, factStartDate, factFinishDate,
        contractDate, nameProjectAdmin,         
        archived ON CHANGE {
            IF archived(p) THEN endDate(p) <- NULL;
            ELSE endDate(p) <- sum(currentDate(), -1);
        },
        warrantyObligations, warrantyStartDate SHOWIF warrantyObligations(p), warrantyFinishDate SHOWIF warrantyObligations(p)    
    
    OBJECTS le = ProjectLegalEntity
    PROPERTIES (le) nameLegalEntity, nameProjectLegalEntityRole,
        NEW, DELETE
    FILTERS project(le) == p
;
DESIGN project{
    header{
        PROPERTY(namePartner(p)) {caption = 'Заказчик';};
        headerRight{
            REMOVE PROPERTY (startDate(p));
            MOVE PROPERTY (nameProjectAdmin(p));
            MOVE PROPERTY (archived(p)) {caption = 'Архивный'; }
        }        
        
        details {
            NEW additionalInfo {
                alignment = STRETCH;
                caption = 'Дополнительно';
                horizontal = TRUE;
                NEW dates{
                    MOVE PROPERTY (changeDate(p));
                    MOVE PROPERTY (endDate(p)) {caption = 'Дата архивирования'; }
                    MOVE PROPERTY (contractDate(p));
                    MOVE PROPERTY (contractStartDate(p));
                    MOVE PROPERTY (contractFinishDate(p));
                    MOVE PROPERTY (planStartDate(p));
                    MOVE PROPERTY (planFinishDate(p));
                    MOVE PROPERTY (factStartDate(p));
                    MOVE PROPERTY (factFinishDate(p));
                }
                NEW warrantyObligations {
                    MOVE PROPERTY (warrantyObligations(p));
                    MOVE PROPERTY (warrantyStartDate(p));
                    MOVE PROPERTY (warrantyFinishDate(p));
                }
            } 
        }
        
    }

    details {
        NEW projectLegalEntityRole {
            caption = badged('Контрагенты', countProjectLegalEntities(p));
            fill = 1;
            
            MOVE BOX(le) {
                fill = 2;
                caption = '';
            }
        }
    }
}

META defineProjectHistoryLine(doc, docLine, class)

    WHEN SETCHANGED(docLine l IS docLine) AND doc(l) = ###doc d
        AND NOT disableHistory() DO {
        NEW h = ###doc##History {
            doc(h) <- d;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            computer(h) <- currentComputer();
            type(h) <- 'Added line';
            description (h) <- '' + index(l);
        }
    }

    WHEN DROPPED(docLine l IS docLine) AND PREV(doc(l)) = ###doc d
        AND NOT disableHistory() DO {
        NEW h = ###doc##History {
            doc(h) <- d;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            computer(h) <- currentComputer();
            type(h) <- 'Removed line';
            description (h) <- '' + PREV(index(l));
        }
    }

    WHEN CHANGED(class(docLine l)) AND doc(l) = ###doc d
        AND NOT disableHistory() DO {
        NEW h = ###doc##History {
            doc(h) <- d;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            computer(h) <- currentComputer();
            type(h) <- 'Product changed';
            description (h) <- '' + index(l) + (CONCAT ' -> ', PREV(name###class(l)), name###class(l));
        }
    }

END

META defineProjectHistoryLineProperty(doc, docLine, prop, capt, descrProp)
    WHEN CHANGED(prop(docLine l)) AND doc(l) = ###doc d
        AND NOT disableHistory() DO {
        NEW h = ###doc##History {
            doc(h) <- d;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            computer(h) <- currentComputer();
            type(h) <- capt;
            description (h) <- '' + index(l) + '. ' + (CONCAT ', ', descrProp(l), (CONCAT ' -> ', PREV(prop(l)), prop(l)));
        }
    }
END