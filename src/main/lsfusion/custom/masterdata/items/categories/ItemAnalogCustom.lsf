MODULE ItemAnalogCustom;

REQUIRE MasterData, ItemCustom;

NAMESPACE MasterData;

dataAnalog 'Аналог' (Item item, Item analog) = DATA BOOLEAN (Item, Item);
analog 'Аналог' (Item item, Item analog) = OVERRIDE item == analog AND item IS Item AND analog IS Item, dataAnalog(item, analog);

CONSTRAINT SETCHANGED(analog(Item i, Item analog)) AND (NOT itemType(i) == itemType(analog) OR i == analog)
    CHECKED BY analog[Item, Item]
    MESSAGE 'Номенклатура не может быть аналогом для текущей';

addAnalog 'Добавить'(Item item) {
    DIALOG items OBJECTS i INPUT 
        FILTERS NOT i == item AND itemType(i) == itemType(item) DO {
        analog(item, i) <- TRUE;
    }    
} IMAGE 'add.png';

deleteAnalog 'Удалить' (Item item, Item analog) {
    analog(item, analog) <- NULL;
} IMAGE 'delete.png';

EXTEND FORM item
    OBJECTS analog = Item
    PROPERTIES(analog) READONLY name, id, nameCategory, nameUom, reference, class
    PROPERTIES TOOLBAR DRAW analog addAnalog(i), deleteAnalog(i, analog)
    ORDERS name(analog)
    FILTERS analog(i, analog)
;

DESIGN item {
    tabs {
        MOVE BOX(analog) { caption = 'Аналоги'; showIf = i IS Product;}
    }
}